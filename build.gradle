plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/releases/"
    }
}

group = "org.embulk"
version = "0.8.0-SNAPSHOT"
description = "Loads records from Mongodb."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.19"
    compileOnly "org.embulk:embulk-spi:0.10.19"
    compile "org.mongodb:mongo-java-driver:3.8.1"

    compile("org.embulk:embulk-util-config:0.1.4") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.10.19"
    testCompile "org.embulk:embulk-core:0.10.19:tests"
    testCompile "org.embulk:embulk-standards:0.10.19"
    testCompile "org.embulk:embulk-deps:0.10.19"
}

embulkPlugin {
    mainClass = "org.embulk.input.mongodb.MongodbInputPlugin"
    category = "input"
    type = "mongodb"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationTo???Repository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    // TODO: Add "repositories" section to publish this plugin into a Maven repository.
}

gem {
    authors = [ "Kazuyuki Honda" ]
    email = [ "hakobera@gmail.com" ]
    summary = "Mongodb input plugin for Embulk"
    homepage = "https://github.com/hakobera/embulk-input-mongodb"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
    outputs.upToDateWhen { false }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
